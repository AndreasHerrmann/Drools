//created on: 13.10.2015
package de.hdm.bahnhofsteuerung

//list any import classes here.
import de.hdm.bahnhofsteuerung.bahnhof.*;
import de.hdm.bahnhofsteuerung.events.*


//declare any global variables here
declare Zugeinfahrt
	@role(event)
end

declare Zugdurchfahrt
	@role(event)
end

declare Zugabfahrt
	@role(event)
end

declare ZugKollision
	@role(event)
end


rule "Zugeinfahrt bekanntgeben"
	no-loop
    when
        $ze: Zugeinfahrt()
    then
        $ze.getGleis().getAnzeige().anzeigen("Der Zug "+$ze.getZug().getNummer()
        +" mit dem Ziel "+$ze.getZiel()+" ist eingefahren."); 
end

rule "Zugabfahrt bekanntgeben"
	no-loop
    when
        $za: Zugabfahrt()
        $ze: Zugeinfahrt(zug==$za.zug)
    then
		$za.getGleis().getAnzeige().anzeigen("Der Zug "+$za.getZug().getNummer()
		+" nach "+$ze.getZiel()+" ist abgefahren");
end

rule "Kollisionen erkennen"
	salience(100)
	no-loop
	when
		$ze1: Zugeinfahrt() and
		$ze2: Zugeinfahrt(this after $ze1 && gleis==$ze1.getGleis()) and
		not ZugKollision(zug1==$ze1.getZug() && zug2==$ze2.getZug() && gleis==$ze1.getGleis()) and
		not Zugabfahrt(this after $ze1 && zug==$ze1.getZug() && gleis==$ze1.getGleis())
	then
		insert(new ZugKollision($ze1.getZug(),$ze2.getZug(),$ze1.getGleis()));
end

rule "Kollision ausgeben"
	salience(100)
	no-loop
	when
		$zk: ZugKollision()
	then
	System.out.println("Die Züge "+$zk.getZug1().getNummer()+" und "
	+$zk.getZug2().getNummer()+ " werden auf Gleis "+$zk.getGleis().getNummer()
	+" kollidieren!");
end

rule "Zugdurchfahrt erkennen"
	no-loop
	when
		$ze: Zugeinfahrt() and
		Zugabfahrt(this after[0s,10s] $ze && $ze.zug==zug && $ze.gleis==gleis)
	then
		insert(new Zugdurchfahrt($ze.getZug(),$ze.getGleis()));
end

rule "Zugdurchfahrt anzeigen"
	no-loop
	when
		$zd: Zugdurchfahrt()
	then
		System.out.println("Zugdurchfahrt: Auf Gleis "+
		$zd.getGleis().getNummer()+" fährt der Zug "+$zd.getZug().getNummer()
		+"durch");
end